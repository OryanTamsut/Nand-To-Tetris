class Cheese{

    field int xPosition;
    field int yPosition;
    field int type;
    field int speed;

    constructor Cheese new(int startPosition) {
        //init the cheese fields
        let xPosition=480;
        let type=0;
        let yPosition=startPosition;
        let speed=100;
        return this;
    }

    /**delete the cheese*/
    method void dispose() {
        do clear();
        do Memory.deAlloc(this);
        return;
    }

    /**increase the speed of the chesse*/
    method void increaseSpeed(){
        if(speed>20){
            let speed=speed-10;
        }
        return;
    }


    /**draw the cheese*/
    method void draw() {
        var int memAddress;
        let memAddress = 16384+((32*(yPosition+16))+((xPosition+16)/16));
        do Memory.poke(memAddress+160, -4096);
        do Memory.poke(memAddress+192, -29696);
        do Memory.poke(memAddress+224, -32000);
        do Memory.poke(memAddress+256, -24384);
        do Memory.poke(memAddress+288, -30416);
        do Memory.poke(memAddress+320, -32760);
        do Memory.poke(memAddress+352, -28124);
        do Memory.poke(memAddress+384, -32766);
        do Memory.poke(memAddress+416, -1);
	    return;
    }
    
    /**clear the cheese*/
    method void clear(){
        var int memAddress;
        let memAddress = 16384+((32*(yPosition+16))+((xPosition+16)/16));
        do Memory.poke(memAddress+160, 0);
        do Memory.poke(memAddress+192, 0);
        do Memory.poke(memAddress+224, 0);
        do Memory.poke(memAddress+256, 0);
        do Memory.poke(memAddress+288, 0);
        do Memory.poke(memAddress+320, 0);
        do Memory.poke(memAddress+352, 0);
        do Memory.poke(memAddress+384, 0);
        do Memory.poke(memAddress+416, 0);
        return;
    }

    /**move the cheese from right to left*/
    method void move() {
        do Sys.wait(speed);
        do clear();
        let xPosition=xPosition - 16;
        do draw();
        return;
    }

    /**change the x position of the cheese*/
    method void setxPosition(int newxPosition){
        do clear();
        let xPosition=newxPosition;
        do draw();
        return;  
    }

    /**get the x position of the cheese*/
    method int getPosition(){
        return xPosition;
    }

    method void setSpeed(int speed2){
        let speed=speed2;
        return;
    }

}
